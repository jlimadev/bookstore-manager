buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-noarg:$kotlinVersion"
    }
}

plugins {
    id 'org.springframework.boot' version "$springBootVersion"
    id 'io.spring.dependency-management' version "1.0.11.RELEASE"
    id 'org.jetbrains.kotlin.jvm' version "$kotlinVersion"
    id "org.jetbrains.kotlin.plugin.spring" version "$kotlinVersion"
    id 'jacoco'
    id "org.barfuin.gradle.jacocolog" version "1.2.4"
}

repositories {
    mavenCentral()
}

apply plugin: 'kotlin'
apply plugin: "kotlin-spring"
apply plugin: "kotlin-jpa"
apply plugin: 'jacoco'
apply plugin: 'org.barfuin.gradle.jacocolog'

group = "com.jlima"
version = "0.0.1-SNAPSHOT"
java.sourceCompatibility = JavaVersion.VERSION_11

dependencies {
    implementation("org.springframework.boot:spring-boot-devtools")
    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("org.springframework.boot:spring-boot-starter-actuator")
    implementation("org.springframework.boot:spring-boot-starter-data-jpa")
    testImplementation("org.springframework.boot:spring-boot-starter-test")

    implementation("io.springfox:springfox-boot-starter:${springfoxVersion}")
    implementation("org.springdoc:springdoc-openapi-ui:${springdocVersion}")
    implementation("org.springdoc:springdoc-openapi-kotlin:${springdocVersion}")

    implementation("org.jetbrains.kotlin:kotlin-reflect")
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
    implementation("com.fasterxml.jackson.module:jackson-module-kotlin")

    runtimeOnly("com.h2database:h2:1.4.200")
    runtimeOnly("org.postgresql:postgresql:42.2.22")
}

compileKotlin {
    kotlinOptions.jvmTarget = '11'
}

test {
    useJUnitPlatform()
    // report is always generated after tests run
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.7"
}

def packagesToExcludeOnCoverage = [
        'com/jlima/bookstoremanager/config/**',
        'com/jlima/bookstoremanager/**/exception/**',
        'com/jlima/bookstoremanager/**/entity/**',
        'com/jlima/bookstoremanager/**/dto/**',
        'com/jlima/bookstoremanager/**/repository/**'
]

jacocoTestReport {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: packagesToExcludeOnCoverage)
        }))
    }
    // tests are required to run before generating the report
    dependsOn test
}

// add to /actuator/info information about the app
springBoot {
    buildInfo()
}

// renames the generated jar file
tasks {
    bootJar {
        archiveFileName.set("bookstore-manager.jar")
    }
}